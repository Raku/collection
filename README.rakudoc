=begin pod

=TITLE Raku Collection Module

=DESCRIPTION A subroutine to collect content files written in Rakudoc (aka POD6).
A distinction is made between the Rakudoc files for the main content (sources) and the Rakudoc
files that describe the whole collection of sources (mode-sources), eg. the landing page (I<index.html>) of a website,
or the Contents page of the same sources in book form.
The collection process is in stages
at the start of which plugins (Raku programs) can be added that transform intermediate data or add
templates, or add new Pod::Blocks for the rendering.

=AUTHOR Richard Hainsworth aka finanalyst

This module is used by Collection-Raku-Documentation, but is intended to be more general, such as building a blog site.

=head1 Installation

    zef install Collection

=head1 Usage

The Collection module expects there to be a C<config.raku >file in the root of the collection, which provides information
about how to obtain the content (Pod6/rakudoc> sources, a default Mode to render and output the collection. All the configuration,
template, and plugin files described below are B<Raku> programs that evaluate to a Hash. They are described in the
documentation for the C<RakuConfig> module.

A concrete example of C<Collection> is the L<Collection-Raku-Documentation (CRD)|https://github.com/finanalyst/collection-raku-documentation.git>
module. I<CRD> contains a large number of plugins (see below). Some plugin examples are constructed
for the extended tests. Since the test examples files are deleted by the final test, try:

    prove6 -I. xt/1* xt/2* xt/3* xt/4* xt/5*

and then look at C<xt/test-dir>.

The main subroutine is C<collect>. It requires a file C<config.raku> to be in the C<$CWD> (current working directory).
In I<CRD> the executable C<Raku-Doc> initiates the collection by setting up sources and installing a C<config.raku>
file. It is then simply a command line interface to C<collect>.

=head1 Life cycle of processing

The content files are processed in several stages separated
by milestones. At each milestone, intermediary data can be processed
using plugins, the data after the plugins can be dumped, or the processed halted.

C<collect> can be called with option flags, which have the same effect as configuration options. The run-time values
of the L<Control flags> take precedence over the configuration options.

C<collect> should be called with a L<Mode>. A B<Mode> is the name of a set of configuration files, templates,
and plugins that control the way the source files are processed and rendered. The main configuration file must
contain a key called C<mode>, which defines the default mode that C<collect> uses if called with no explicit
mode, so if C<collect> is called without a B<Mode>, the default will be used.

=head1 Milestones

The C<collect> sub can be called once the collection directory contains a C<config.raku>,
which in turn contains the location of a directory of rakudoc source files,
which must contain recursively at least one source.

Processing occurs during a stage named by the milestone which starts it.
Each stage is affected by a set of L<Control flags>.
Certain flags will be passed to the underlying objects, eg. C<RakuConfig> and C<ProcessedPod> (see
C<Raku::Pod::Render>).

Plugins may be called at each milestone (except 'Source' and 'Mode', where they are not defined).
Plugins are described in more detail in L<Plugin management>. Plugins are milestone specific, with the call
parameters and return values depending on the milestone.

=head2 Stopping or dumping information at milestones

Intermediate data can be dumped at the milestone
B<without stopping> the processing, eg.,

    collect(:dump-at<source render>);

Alternatively, the processing can be B<stopped> and intermediate data inspected,
EITHER after the stage has run,
but before the plugins for the next stage have been triggered, eg.,

    my $rv = collect(:after<setup>);

OR after the previous stage has run and after the plugins for the milestone have been triggered, eg.,

    my $rv = collect(:before<render>);

The return value C<$rv> is an array of the objects provided to plugins at that milestone, and an array of
the plugins triggered (note the plugins used will be a difference between the C<:before> and C<:after> stop points).
The plugins-used array is not provided to all plugins, except at the Report milestone.

The return value C<$rv> at C<:after> will contain the object provided by the milestone after the named milestone.
For example, milestone milestone 'Setup' is followed by milestone 'Render'.
The return object for C<:after<setup>> will be the
return object for milestone 'Render'. See Milestones for more information.

The object returned by C<:before<render>> may be affected by the plugins that are triggered before the named stage.

The C<:before>, C<:after> and C<:dump-at> option values are the (case-insensitive) name(s) of the inspection point for the milestone.
C<:before> and C<:after> only take one name, but C<:dump-at> may take one or all of them in a space-delimited unordered list.

The C<dump-at> option calls C<.raku>
[TODO pretty-dump, when it handles BagHash and classes] on the same objects as above
and then outputs them to a file(s) called C<dump-at-<milestone name>.txt>.

=head2 Source Milestone

(Skipped if the C<:without-processing> flag is True)

Since this is the start of the processing, no plugins are defined as there are no objects for them to operate on.

The C<config.raku> file must exist and must contain a minimum set of keys. It may optionally contain keys for
the control flags that control the stage, see below. The intent is to keep the options for the root configuration file
as small as possible and only refer to the source files. Most other options are configured by the Mode.

During the subsequent B<Source> stage, the source files in the collection are brought in, if the collection has not been
fully initiated, using the C<source-obtain> configaturation list. Alternatively, any updates are brought in
using the C<source-refresh> list. Commonly, sources will be in a I<git> repository, which has separate commands for
C<clone> and C<pull>. If the C<source-obtain> and C<source-refresh> options are not given (for example during a test),
no changes will be made to the source directory.

Any changes to the source files are cached by default.

The control flags for this stage are:
=begin item
B<no-refresh> (default False)

Prevents source file updates from being brought in.

=end item
=begin item
B<recompile>

Forces all the source files to be recompiled into the cache.

=end item

=head2 Mode Milestone

(Skipped if the C<:without-processing> flag is True)

Collection makes a distinction between Rakudoc source files that are the main content, and the source files needed to
integrate the main content into a whole. The integration sources will differ according to the final output. For example,
a book may have a Foreward, a Contents, a Glossary, etc, whilst a website will have a landing page (eg., I<index.html>),
and perhaps other index pages for subsections. A book may also organise content into sections that depend on metadata in
the source files. A book will have a defined order of sections, but a website has no order. A website will require
CSS files and perhaps jQuery scripts to be associated with Blocks. A book will have different formating requirements for
pages.

These differences are contained in the B<mode> configuration, and the plugins and templates for the mode.

At this milestone, the source files have been cached. The B<mode> sub-directory has not been tested, and the configuration
for the mode has not been used. Since plugin management is dependent on the mode configuration, no plugins can be called.

The B<return value> of C<collect> with C<:after<source>> is a single
C<Pod::From::Cache> object that does a C<Post-Cache> role (see below for C<Post-Cache> methods).

A C<Pod::From::Cache> object provides a list of updated files, and a full set of source files. It will
provide a list of Pod::Blocks contained in each content files, using the filename as the key.

During the stage the source files for the Mode are obtained, compiled and cached. The process is controlled by the
same options as the I<Source> stage. For example, the B<Mode> for C<Collection-Raku-Documentation> is I<Website>.

If a sub-directory with the same name as I<mode> does not exist, or there are no config files in the C< <mode>/config> directory,
C<collect> will throw an C<X::Collection::NoMode> exception at the start of the stage.

Mode source files are stored under the mode sub-directory and cached there. If the mode source files are stored remotely and
updated independently of the collection, then the C<mode-obtain> and C<mode-refresh> keys are used.

=head2 Setup Milestone

(Skipped if the C<:without-processing> flag is True)

If B<setup> plugins are defined and in the mode's plugins-required<setup> list, then the cache objects for
the sources and the mode's sources (and the B<full-render> value)
are passed to the program defined by the plugin's B<setup> key.

The purpose of this milestone
is to allow for content files to be pre-processed, perhaps to creates several sub-files from one big file, or to combine
files in some way, or to gather information for a search algorithm.

During the setup stage,
=item the C<ProcessedPod> object is prepared,
=item templates specified in the C<templates> directory are added
=item the key B<mode-name> is added to the C<ProcessedPod> object's plugin-data area and given the value of the mode.

The Setup stage depends on the following options:
=begin item
B<full-render>

By default, only files that are changed are re-rendered, which includes an assumption that if any source file
is changed, then all the B<mode> sources must be re-rendered as well. (See the Problems section below for a caveat.)

When B<full-render> is True, the output directory is emptied of content, forcing all files to be rendered.

B<full-render> may be combined with B<no-refresh>, for example when templates or plugins are changed and the aim
is to see what effect they have on exactly the same sources. In such a case, the cache will not be changed, but
the cache object will not contain any files generated by B<setup> plugins.
=end item

=head2 Render Milestone

(Skipped if the C<:without-processing> flag is True)

At this milestone C<render> plugins are supplied to the
C<ProcessedPod> object. New Pod::Blocks can be defined, and the templates associated with them can be created.

The source files (by default only those that have been changed) are rendered. 

The stage is controlled by the same options as I<Setup>.

=head2 Compilation Milestone

(Skipped if the C<:without-processing> flag is True)

At this milestone plugins are provided to add compiled data to the C<ProcessedPod> object, so that the sources
in the mode's directory can function.

During the B<Render> stage, the C<%processed> hash is constructed whose keys are the filenames of the output files, and whose
values are a hash of the page components of each page.

The C<compilation> plugins could, eg, collect page component data (eg., Table of Contents, Glossaries, Footnotes),
and write them into the C<ProcessedPod> object separately so there is a TOC, Glossary, etc structure whose keys
are filenames.

The return value of C<collect> at the inspection point is a list of C<ProcessedPod>, C<%process>, with the C<ProcessedPod>
already changed by the C<compilation> plugins.

=head2 Transfer Milestone

(Skipped if the C<:without-processing> flag is True)

Plugins may generate assets that are not transfered by them, or it is important to ensure
that a plugin runs after all other plugins.

In addition, render plugins may create files that are transfered at the render stage, but should
be removed after all plugins have run. So a transfer milestone plugin can be created to
clean up the plugin's local directory.

=head2 Report Milestone

(Skipped if the C<:without-processing> flag is True)

Once a collection has been rendered, all the links between files, and to outside targets can be subjected to
tests. It is also possible to subject all the rendered files to tests. This is accomplished using C<report> plugins.

In addition, all the plugins that have been used at each stage (except for the Report stage itself) are listed.
The aim is to provide information for debugging.

The report stage is intended for testing the outputs and producing reports on the tests.

=head2 Completion Milestone

Once the collection has been tested, it can be activated. For example, a collection could be processed into a book,
or a C<Cro> App run that makes the rendered files available on a browser. This is done using C<completion> plugins.

The B<without-completion> option allows for the completion phase to be skipped.

Setting B<without-processing> to True and B<without-completion> to True should have no effect unless
=item there are no caches, which will be the case the first time C<collect> is run
=item the destination directory is missing, which will be the case the first time C<collect> is run

=head2 Cleanup Milestone

Cleanup comes after C<collect> has finished, so is not a part of C<collect>.

Currently, C<collect> just returns with the value of the @plugins-used object.

[This API may change if a use is found for Cleanup]

=head1 Collection Structure

A distribution contains content files, which may be updated on a regular basis, a cache,
templates, extra assets referenced in a content file (such as images), and one or more modes.

=head2 Collection Content

The content of the distribution is contained in B<rakudoc> files. In addition to the source files, there are
Collection content files which express things like the Table of Contents for the whole collection.

Collection content are held separately to the source content, so that each mode may have different pages.

This allows for active search pages for a Website, not needed for an epub, or publisher data for an output
formation that will be printed.

=head2 Extra assets (images, videos, etc)

Assets such as images, which are directly referenced in content file, but exist in different formats, eg, png,
are held apart from content Pod6 files, but are processed with content files.

The reasoning for this design is that Pod6 files are compiled and cached in a manner that does not suit
image files. But when an image file is processed for inclusion in a content file, the image may need to be
processed by the template (eg., image effects specified in a Pod Block config).

The assets are all held in the same directory, specified by the configuration key C<asset-basenamme>, but each asset
may exist in subdirectories for each type of asset, specified by the C<asset-paths> key.

(Asset files relating to the rendering of a content file, such as css, javascript, etc, are managed by
plugins, see below for more on plugins.)

A class to manage asset files is added to the C<ProcessedPod> object with a role, so the assets can be manipulated by
plugins and templates. Assets that are in fact used by a Pod content file are marked as used. The aim of
this functionality is to allow for report-stage plugins to detect whether all images have been used.

Plugins can also transform the assets, and create new files in the ProcessedPod object for inclusion in
the output.

At the end of the compilation stage, all the assets that have been used are written to a directory
specified in the Mode configuration file. It is the task of the template rendering block to ensure that the
path where the asset is stored is the same as the path the final output (eg. the browser rendering html files)
processor requests.

In keeping with the principle that collection level meta data is kept in the top-level config file, and output data is
associated with the specific mode, there are two asset-path definitions.
=begin item
Collection level assets. The source of assets is kept in the top-level C<config.raku> file. In order to have each asset
in its own directory, the following is possible:

    ...
    :asset-basename<assets>,
    asset-paths => %(
        image => %(
            :directory<images>,
            :extensions<png jpeg jpeg svg>,
        ),
        video-clips => %(
            :directory<videos>,
            :extensions<mp4 webm>,
        ),
    ),
    ...

Notice that the C<type>, eg. I<image> and I<video-clips> above, are arbitrary and not dependent on the actual format.

=end item
=begin item
Output configuration. The output destination is kept in the mode  configuration, eg., C<Website/configs/03-images.raku> contains

    %(
        :asset-out-path<html/assets>
        ),
    )

For more see L<Asset-cache methods>

=end item

=head2 Cache

The B<cache> is a Precomp structure into which the content files are pre-preprocessed.

=head2 Mode

The B<Mode> is the collection of templates and configuration for some output. A collection may contain
multiple Modes, each in their own subdirectory.

The default Mode for B<Collection-Raku-Documentation> is B<Website>, for example.

The string defining C<mode> must refer to an immediate directory of the root of the collection,
so it is compared to C</ ^ \W+ (\w+) '/'? .* $ /> and
only the inner C<\w> chars are used.

The templates, configuration, output files, and other assets used by a Mode are associated with the Mode, and should
reside beneath the Mode sub-directory.

=head2 Templates

The B<templates>, which may be any format (currently RakuClosure or Mustache) accepted by
C<ProcessedPod>, define how the following are expressed in the output:
=item the elements of the content files, eg. paragraphs, headers
=item the files as entities, eg, whether as single files, or chapters of a book
=item the collective components of content files, viz, Table of Contents, footnotes, Glossary, Meta data
=item All the templates may be in one file, or distributed between files.
=item2 If there are no templates in the directory, the default files in C<ProcessedPod> are used.
=item2 If there are multiple files in the directory, they will all be evaluated in alphanumeric order.
Note that existing keys will be over-written if they exist in later templates. This is B<not> the same
behaviour as for Configuration files.

=head1 Configuration

There are two levels of configuration. The top-level resides in C<config.raku> in the root directory of the
Collection. The C<collect> sub will fail without this file.

=head2 Top level configuration

In the descriptions below, simple illustrative names are given to files with configuration, templates, callables.
These files are generally B<Raku> programs, which are compiled and run. They will almost certainly contain errors
during development and the B<Rakudo> compiler will provide information based on the filename. So it is good
practice to name the files that make them easier to locate, such as prefixing them with the plugin name.

C<config.raku> B<must> contain the following keys:
=item the B<cache> directory, relative to the root directory of the collection
=item2 C<Collection-Raku-Documentation> default: 'doc-cache',
=item the B<sources> directory, relative to the root of the collection and must contain at least one content file
=item2 C<Collection-Raku-Documentation> default: 'raku-docs'
=item B<mode> is the default mode for the collection, and must be a sub-directory, which must exist and contain
a C<configs> sub-directory (note the plural ending).
=item2 C<Collection-Raku-Documentation> default: 'Website'

The following are optional keys, together with the defaults 
=item the allowed B<extensions> for content files. These are provided to the C<Pod::From::Cache> object.
=item2 default: < rakudoc pod pod6 p6 pm pm6 >
=item no-status This option controls whether a progress bar is provided in the terminal
=item2 default: False
=item B<source-obtain> is the array of strings sent to the OS by C<run> to obtain sources, eg git clone
and assumes CWD is set to the directory of collection. Without this key, there must already be files
in C<sources>.

=item2 default: ()
=item B<source-refresh> is the array of strings run to refresh the sources, assumes CWD set to the directory of sources.
No key assumes the sources never change.

=item2 default: ()
=item B<ignore> is a list of files in the B<sources> directory that are not cached.
=item2 default: ()
=item B<no-status> as described in Milestones
=item2 default: False
=item B<without-processing> as described in Milestones
=item2 default: False
=item B<no-refresh> as described in Milestones
=item2 default: False
=item B<recompile> as described in Milestones
=item2 default: False

=head2 Second-level configuration

The second-level configuration resides in one or more B<files> that are under the B<configs/> sub-directory of the
C<mode> directory. This arrangement is used to allow for configuration to be separated into different named
files for ease of management.

The following rules apply:
=item If the B<configs> directory for a mode does not exist or is empty, B<Raku-Doc> (C<collect> sub) will fail.
=item The Configuration consists of one or more C<Raku> files that each evaluate to a hash.
=item2 Each Config file in the B<Configs> directory will be evaluated in alphabetical order.
=item2 Configuration keys may not be over-written. An C<X::RakuConfig::OverwriteKey> exception will be thrown if
an attempt is made to redefine a key.

All the following keys are mandatory.
Where a key refers to a directory (path), it should normally be relative to the C<mode> sub-directory.
=item B<mode-sources> location of the source files for the Collection pages, eg., TOC.
=item B<mode-cache> location of the cache files
=item the B<templates> subdirectory, which must contain raku files as described in C<ProcessedPod>.
These are all passed at the B<Render> milestone directly to the C<ProcessedPod> object.

=item B<destination> directory where the output files are rendered
=item B<plugins> is a string with the location of the plugins directory, either relative to
root of the mode directory, or an absolute path. It is possible for the plugins directory to contain
unused plugins. See L<Plugin management>

=item B<report-path> is the path to which C<report> plugins should output their reports.
=item B<plugins-required> points to a hash whose keys are milestone names where plugins can be applied
=item2 B<setup> a list of plugin names, see L<Plugin management>, for pre-processing cache contents
=item2 B<render> plugins used to render Pod::Blocks
=item2 B<compilation> plugins prepare the C<ProcessedPod> object for collection pages.
=item2 B<assets> plugins that mark assets created in previous milestones
=item2 B<report> plugins to test and report on the rendering process
=item2 B<completion> plugins that define what happens after rendering
=item2 B<cleanup> plugins if cleanup is needed.
=item B<landing-place> is the name of the file that comes first during the completion stage. For example,
in a Website, the landing file is usually called C<index.html>
=item B<output-ext> is the extension for the output files

All optional control flags are False by default. They are:
=item no-status
=item recompile
=item no-refresh
=item full-render
=item without-completion
=item without-report
=item without-processing
=item no-preserved-state
=item debug-when
=item verbose-when
=item no-code-escape

=head1 Control flags

The control flags are also covered in L<Milestones>. Control flags by default are False.

=begin item
B<no-status>

No progress status is output at any time.
=end item
=begin item
B<without-processing>

Setting B<without-processing> to True will skip all the stages except
B<Completion>, so long as the destination directories exist.

=end item
=begin item
B<no-preserved-state>

In order to allow for changes in some source files, or in only mode files, after all the sources
have been processed once, the processing state must be archived. This may not be needed in testing
or if the archiving takes too long.

Setting no-preserved-state = True prevents storage of state, but also forces B<without-processing> to False,
and B<recompile> to True.
=end item
=begin item
B<recompile>

Controls the updating and caching of the content files. If true, then all files will be recompiled and cached.

A True value is over-ridden by B<without-processing>

Normally False, which allows for only changed files to be processed.

=end item
=begin item
B<no-refresh>

Prevents the updating of content files, so no changes will be made.

=end item
=begin item
B<full-render>

Forces all files to be rendered. Even if there are no changes to source files, plugins or templates
may be added/changed, thus changing the output, so all files need to be re-rendered.

This flag is set to False if B<without-processing> is True.

=end item
=begin item
B<without-report>

Normally, report plugins report on the final state of the output files. This flag prevents
report plugins from being loaded or run.

If B<without-processing> is set, then the B<Report> stage is skipped. If, however,
the caches do not exist (deleted or first run), then the value of B<without-processing> is ignored and
the value of B<without-report> is observed.

=end item

=begin item
B<debug-when & verbose-when>

ProcessedPod uses C<debug> and C<verbose>, which provide information about which blocks are processed (debug),
and the result after the application of the template (verbose). This is a lot of information and generally, it is only
one file that is of interest.

These two flags take a string, eg., C<:debug-when<Introduction.pod6> >, and when the filename matches
the string, then the debug/verbose flag is set for that file only. (verbose is only effective when
debug is True).

=end item
=begin item
B<collection-info>

Causes collect to produce information about milestones and valid and invalid plugins
=end item
=begin item
B<with-only> filename

Collect is run only with that filename, which must be in the sources or mode-sources, and is specified like C<debug-when>.

The option takes a string containing the filename. An empty string means all filenames in sources and mode-sources.
=end item

=begin item
B<no-code-escape>

C<ProcessedPod> has a special flag for turning off escaping in code sections
when a highlighter is used to pre-process code. In some cases, the highlighter also
does HTML escaping, so RPR has to avoid it.

This has to be done at the Mode level and not left to C<render> plugins.
=end item
=head1 Plugin management

Plugins are B<Raku> programs that are executed at specific milestones in the rendering process.
The milestone names are given in L<Milestones> above.

The B<plugins-required> key in the Mode's configuration contains a hash with keys whose names are the
milestone names. Each key points to a list of plugin names, which are called in the order
given.

All plugins must reside within the mode directory given by C<plugins>, but this directory may belong
to another Collection so that plugins could be shared between collections & modes. [TODO Revise plugin management
so that common plugins can be maintained and developed separately to Collections].

All plugin names B<must> be the name of a sub-directory of the B<plugins> path. Within each plugin sub-directory,
there must be a C<config.raku> file containing information for the plugin, and for C<Collection>.
If I<no> C<config.raku> files exists, the plugin is not valid and will be skipped.

With the exception of 'render' plugins, the config file must contain a key for the milestone type,
which points to the program to be called, and when the file is evaluated, it yields a subroutine that
takes the parameters needed for the plugin of that milestone. If no key exists with the name of the milestone,
then the plugin is not valid.

Plugin's may need other configurable data, which should be kept in the config file for the plugin.

All plugins are expected to adhere to C<no-status> and C<collection-info>, which are interpretted as
=item C<no-status> if true means 'no output at all', equivalent to a B<quiet> flag
=item C<collection-info> if true means 'output extra information' (if need be), equivalent to a B<verbose> flag.

=head2 Disabling a plugin

When it's necessary to disable a plugin, this can be done by:
=item Removing the plugin name from the C<plugins-required> key of the Mode's config file;
=item Renaming / removing the C<config.raku> file name inside the plugin directory
=item Renaming / removing the milestone key inside the plugin's C<config.raku> file.

=head2 Plugin types

The plugin types are as follows.

=head3 Setup

Config hash must contain B<setup> which is the name of a Raku
program (a callable) that evaluates to a sub that takes a list of five items, eg.,

    sub ( $source-cache, $mode-cache, Bool $full-render, $source-root, $mode-root, %options ) { ... }

=defn $source-cache
A C<Pod::From::Cache+PostCache> object containing the pod of the sources files

=defn $mode-cache
Like the above for the mode content files

=defn $full-render
If True, then the sub should process the cache objects with the .sources method on the cache objects,
otherwise with the .list-files method on the cache objects (the .list-files method only
provides the files that have changed).

=defn $source-root
This path must be prepended to any sources added (see below) to the cache, otherwise they will not be added to the
destination file.

=defn $mode-root
Likewise for the mode sources.

=defn %options
Has the values of 'collection-info' and 'no-status' flags.

New files can be added to the cache object inside the sub using the C<.add> method, see L<Sources>.

=head3 Render

The Collection plugin-manager calls the C<ProcessedPod.add-plugin> method with the config keys and
the path modified to the plugin's subdirectory.

If the C<render> key is True, no callable is provided, and the plugin name will be added via the B<.add-plugin>
method of the C<ProcessedPod> object. See C<ProcessedPod> documentation.

If the C<render> key is a Str, then it is the filename of a Raku callable of the form

    sub ( $pr, %options --> Array ) {...}

where

=item B<$pr> is a <ProcessedPod> object,
=item B<%options> is the same as for Setup, and
=item the callable B<returns> a list of triples, with the form (to, from-plug, file)
=item2 B<to> is the destination under the C<%config<destination> > directory where the asset will be
looked for, eg., an image file to be served.

=item2 B<from-plug> is the name of the plugin in whose directory the asset is contained, where the value C<myself>
means the path of the plugin calling the render callable. Actually, 'myself' is the value of Collection::MYSELF.

=item2 B<file> is the filename local to the source plugin's subdirectory that is to be copied to the destination.
This may contain a path relative to the plugin's subdirectory.

=item2 For example for an HTML file, this would be the relative
URL for the C<src> field. Eg., C<to = 'asset/image'>, C<file = 'new-image.png'>, C<from-plug = 'myself'>
and the html would be C< <img src="asset/image/new-image.png" /> >.

Since a render plugin is to be added using the C<ProcessedPod> interface, it must have the C<custom-raku> and
C<template-raku> keys defined, even if they evaluate to blank (eg. C<:custom-raku()> ).

So the config file must have:
=item render (True | name of callable)
=item custom-raku => a Raku program that evaluates to an array of custom blocks (must be set to C<()> if no Raku program )
=item template-raku => a Raku program that evaluates to a hash of RakuClosure templates (must be set to C<()> if no Raku program)

It is possible to specify C<path> but it must be relative to the plugin's sub-directory.

More information about these plugins can be found in the documentation in the C<Raku::Pod::Render> distribution.

=head3 Compilation

Note that the structure files are rendered after the C<compilation> stage, BUT the information for rendering
the structure files, that is the custom blocks and the templates must accompany a C< render > plugin.
Compilation plugins are to process the data accumulated during the rendering of the content files,
and to make it available for the custom blocks / templates that will be invoked when the structure documents
are rendered.

The C<compilation> key must point to a Raku program that delivers a sub object

    sub ( $pr, %processed, %options) { ... }

=defn $pr
is the ProcessedPod object rendering the content files.

=defn %processed
is a hash whose keys are source file names with a hash values containing
TOC, Glossary, Links, Metadata, Footnotes, Templates-used structures produced by B<ProcessedPod>.

=defn %options
as for setup

=head3 Transfer

The C<transfer> key points to a Raku file that evaluates to a

    sub ($pr, %processed, %options --> Array ) {...}

=defn %processed
as in Compilation

=defn $pr
as in Compilation

=defn %options
as for Setup

=defn return object
as for the render plugin

=head2 Report

The C<report> key points to a Raku file that evaluates to a

    sub (%processed, @plugins-used, $pr, %options --> Array ) {...}

=defn %processed
as in Compilation

=defn @plugins-used
is an array of Pairs whose key is the milestone and
value is a hash of the plugins used and their config parameters.

=defn $pr
as in Compilation

=defn %options
as for Setup

The plugin should return an Array of Pair, where for each Pair
.key = (path/)name of the report file with extension,
and .value is the text of the report in the appropriate format

The C<collect> sub will write the file to the correct directory.

=head3 Completion

The C<completion> key points to a Raku file that evaluates to
a C<sub ($destination, $landing-place, $output-ext, %completion-options, %options) {...}> object.

=begin item
B<$destination>

is the name of the output path from the mode directory (defined in the mode configuration)

=end item
=begin item
B<$landing-place>

is the first file to be processed since, eg., for a website, order is not sufficient. name is relative to the
destination directory.

=end item
=begin item
B<%completion-options> (actually specified as %config<completion-options>)

is the set of options that the completion plugin will require from the Mode-level configuration. For example,
the very simple C<cro-run> plugin requires the path to the static html files, the hostname, and the port on which the files
are served. More complex plugins will require more options.

=end item

=begin item
B<%options>

As for Setup
=end item

There is no return value specified for this plugin type.

=head1 Problems and TODO items

=head2 Archiving and Minor Changes

In principle, if a small change is made in a source file of a Collection, only the rendered version
of that file should be changed, and the Collection pages (eg., the index and the glossaries) updated.
The archiving method chosen here is based on C<Archive::Libarchive> and a C<.7z> format. It works in
tests where a small quantity of data is stored.

B<However>, when there are many source files (eg., the Raku documentation),
the process of restoring state information is B<significantly> longer
than re-rendering all the cached files. Consequently, the option C<no-preserve-state> prevents
the archiving of processed state. (TODO understanding and optimising the de-archiving process.)

=head2 Plugin development

The aim is to have plugins developed and maintained separately. This may require some change to the Collection
API.

=head2 Dump file formatting

The aim is to use C<PrettyDump> instead of <.raku> to transform information into text. However, <PrettyDump> does
not handle C<Bags> properly.

=head1 Post-cache methods

Post-cache is a role added to a C<Pod::From::Load> object so that Setup plugins can act on Cache'd content
by adding pod files to the Cache (perhaps pre-processing primary source files) that will be rendered,
masking primary pod files so that they are not rendered, or aliasing primary pod files.

If a secondary source file in the Cache is given a name that is the same as a primary source file,
then if the underlying cache object should remain visible, another name (alias) should be given to the
file in the Post-cache database.

The Post-cache methods C<sources>, C<list-files>, and C<pod> have the same function and semantics as
C<Pod::From::Cache> except that the post-cache database is searched first, and if contents are found there,
the contents are returned (which is why post-cache file names hide primary file names).
If there is no name in the Post-cache
database, then it is passed on to the underlying cache.

=head2 multi method add(Str $fn, Array $p --> Pod::From::Cache )

Adds the filename $fn to the cache. $p is expected to be an array of Pod::Blocks, but no check is made. This is
intentional to allow the developer flexibility, but then a call to C<pod( $fn )> will yield an array that is not
POD6, which might not be expected.

The invocant is returned, thus allowing add to be chained with mask and alias.

=head2 multi method mask(Str $fn --> Pod::From::Cache)

This will add only a filename to the database, and thus mask any existing filename in the underlying cache.

Can be chained.

=head2 multi method add-alias(Str $fn, Str :alias!  --> Pod::From::Cache)

This will add a filename to the database, with the value of a key in the underlying cache.
Chain with mask to prevent the original spelling of the filename in the underlying cache being visible.

Can be chained.

If the alias is already taken, an exception is thrown. This will even occur if the same alias
is used for the same cached content file.

=head2 method behind-alias(Str $fn --> Str )

Returns the original name of the cached content file, if an alias has been created, otherwise
returns the same filename.

=head2 method pod(Str $fn)

Will return
=item an array of Pod::Block (or other content - beware of adding other content) if the
underlying Cache or database have content,

=item the array of Pod::Block in an underlying filename, spelt differently

=item C<Nil> if there is no content (masking an underlying file in Cache)

=item throw a NoPodInCache Exception if there is no pod associated with either the database
or the underlying cache. If the original filename is used after an alias have been generated,
the Exception will also be thrown.

=head1 Asset-cache methods

Asset-cache handles content that is not in Pod6 form. The instance of the Asset-cache class is passed via the
plugin-data interface of C<ProcessedPod>, so it is available to all plugins after the setup milestone,
for example in the plugin callable:

    sub render-plugin( $pp ) {
        my $image-manager = $pp.get-data('image-manager');
        ...
        $pp.add-data('custom-block', $image-manager);
    }

By creating a name-space in the plugin data section and assigning it the value of the image-manager,
the plugin callable can make the image-manager available to templates that get that data, which is a property
in parameters called by the name-space.

C<ProcessedPod> provides data from the name-space of a Block, if it exists, as a parameter to the
template called for the Block. Note that the default name-space for a block is
all lower-case, unless a C<name-space> config option is provided with the
Pod Block in the content file.

If a plugin provides an asset (eg., image, jquery script), it needs to provide a C<render> callable
that returns the triple so that Collect moves the asset from the plugin directory to the output
directory where it can be served. This needs to be done separately if a CSS contains a url for
local image.

C<$image-manager> is of type Asset-cache, which has the following methods:

=begin code
    #| the directory base, not included in filenames
    has Str $.basename is rw;
    #| the name of the file being rendered
    Str $.current-file
    #| asset-sources provides a list of all the items in the cache
    method asset-sources
    #| asset-used-list provides a list of all the items that referenced by Content files
    method asset-used-list
    #| asset-add adds an item to the data-base, for example, a transformed image
    method asset-add( $name, $object, :$by = (), :$type = 'image' )
    #| returns name / type / by information in database (not the object blob)
    method asset-db
    #| remove the named asset, and return its metadata
    method asset-delete( $name --> Hash )
    #| returns the type of the asset
    method asset-type( $name --> Str )
    #| if an asset with name and type exists in the database, then it is marked as used by the current file
    #| returns true with success, and false if not.
    method asset-is-used( $asset, $type --> Bool )
    #| brings all assets in directory with given extensions and with type
    #| these are set in the configuration
    multi method asset-slurp( $directory,  @extensions, $type )
    #| this just takes the value of the config key in the top-level configuration
    multi method asset-slurp( %asset-paths )
    #| with type 'all', all the assets are sent to the same output directory
    multi method asset-spurt( $directory, $type = 'all' )
    #| the value of the config key in the mode configuration
    multi method asset-spurt( %asset-paths )
=end code

=head1 Copyright and License

(c) Copyright, 2021-2022 Richard Hainsworth

B<LICENSE> Artistic-2.0

=end pod