=begin pod

=TITLE Raku Collection Module

=DESCRIPTION A subroutine to collect content files written in POD6. The collection process is in stages
at the start of which plugins (Raku programs) can be added that transform intermediate data or add
templates, or add new Pod::Blocks for the rendering.

=AUTHOR Richard Hainsworth aka finanalyst

This module is used by Collection-Raku-Documentation, but is intended to be more general, such as building a blog site.

Can be used as a docker container C<finanalyst/collection>.

=head1 Installation

    zef install Collection

or using docker in a C<Dockerfile>

    FROM finanalyst/collection

=head1 Usage

The Collection module expects there to be a C<config.raku >file in the root of the collection, which provides information
about how to obtain the content (Pod6/rakudoc> sources, a default Mode to render and output the collection. All the configuration,
template, and plugin files described below are B<Raku> programs that evaluate to a Hash. They are described in the
documentation for the C<RakuConfig> module.

A concrete example of C<Collection> is the L<Collection-Raku-Documentation|https://github.com/finanalyst/collection-raku-documentation.git>
module. It provides the C<Raku-Doc> executable, which
copies a C<config.raku> file and a mode called C<Website>. The configuration describes how to get the B<Raku Doc> files
from the B<Raku.org> repository, and the B<Website> mode contains default templates and plugins to create a website
that shows the collection, using a C<Cro> app.

The C<Collection> module provides the infrastructure, whilst C<Collection-Raku-Documentation> provides the concrete configuration
and specifies how files are rendered. However, C<Collection> has been designed so that B<Templates> and B<Plugins>
for C<Collection-Raku-Documentation>
can be used for other collections, while other Collection distributions that only provide B<Plugins> and/or B<Templates>.
Once the Raku-Documentation
collection has been initialised, Raku-Doc calls C<collect>, which is the entry point for C<Collection>.

=head2 Sample Dockerfile

The C<finanalyst/collection> contains a recent Rakudo-Star distribution, including zef, node, and npm. Highlighting
has been enabled in Raku::Pod::Render.

Once the collection has been properly established, by running C<Raku-Doc Init> from the Collection-Raku-Documentation
distribution, for example, then the Collection can be containerised with the C<finanalyst/collection> docker image.

Assuming (for the Collection-Raku-Documentation distribution, which has all the necessary plugins specified, see below) that
=item the intention is to update the Doc-Sources, by pulling in changes from the main repository,
=item re-render only html files that have changed,
=item and serve the html files are served on port 30000 (which is given in the mode's configuration)
then a Dockerfile for Raku-Documentation would be:

    FROM finanalyst/collection
    mkdir /raku-dox
    cd /raku-dox
    COPY . .
    zef install . --deps-only
    CMD bin/Raku-Doc --!no-status

Running this Dockerfile in the distribution's directory after the collection has been set up, will create
a docker image that can be be built.

    sudo docker build -t my-raku-documentation

Then the following will refresh the documents and provide access to them at C<localhost:30000>

    sudo docker run my-raku-documentation

As normal the port can be changed with the -p option, eg, to 54321

    sudo docker run -p 54321:30000 my-raku-documentation

=head1 Life cycle of processing

After initialisation, which should only occur once, then the content files are processed in several stages separated
by milestones. At each milestone, intermediary data can be reprocessed
using plugins, the data after the plugins can be dumped, or the processed halted.

C<collect> can be called with option flags, which have the same effect as configuration options. The run-time values
of the L<Control flags> take precedence over the configuration options.

In B<Collection-Raku-Documentation> C<Raku-Doc> is an Command Line Interface for giving run time options to C<collect>.

C<collect> can also be called with a L<Mode>. A B<Mode> is the name of a set of configuration files, templates,
and plugins that control the way the source files are processed and rendered. The main configuration file must
contain a key called C<mode>, which defines the default mode that C<collect> uses if called with no explicit
mode.

For example, the B<Collection-Raku-Documentation> is set up with a
default C<mode> called B<Website>. C<Raku-Doc> just calls C<collect> and passes on to C<collect> all of
its arguments, with the exception of the string B<Init>, which C<Raku-Doc> traps so that processing can stop before
calling C<collect>.

If C<Raku-Doc> is called with a string other than 'Init' or 'Website', then the string is interpreted as another B<Mode>,
with its own sub-directory and L<Configuration> for the collection. For example,

    Raku-Doc Book

would create the collection output defined by the configuration in the sub-directory C<Book/config/>. This design
is to allow for the creation of different Collection outputs to be defined for the same content files.

=head1 Milestones

The C<collect> sub can B<only> be called cnce the collection directory contains a C<config.raku>,
which in turn contains the location of a directory, which must contain recursively at least on source.

The process of collecting, rendering and outputting the collection has a number of defined milestones. A milestone
will have an inspection point, at which the intermediate data can be dumped B<without stopping> the processing, eg.,

    collect(:dump-at<post-cache render>);

or the processing can be B<stopped> and the intermediate data inspected, eg.

    my $rv = collect(:end<source>);

The C<end> and C<dump-at> option values are the (case-insensitive) name(s) of the inspection point for the milestone.
Clearly, C<end> only takes one name, but dump may take one or all of them (each separately specified).

A milestone may also be where plugins (aka call-backs) can be defined.
Plugins are described in more detail in L<Plugin management>.

The return value of C<collect> at a milestone is the object
provided to the plugins after all the plugins have been evaluated. The aim of this design is to give to
developers the ability to test the effect of plugins at each stage on the object to be modified by
the plugins.

The C<dump-at> option calls C<pretty-dump> or C<.raku> [TODO pretty-dump, when it handles BagHash and classes] on the same objects as above
and then outputs them to a file(s) called C<dump-at-<milestone name>.txt>.

Processing occurs during a stage named by the milestone which starts it.
Each stage is affected by a set of L<Control flags>.
Certain flags will be passed to the underlying objects, eg. C<RakuConfig> and C<ProcessedPod> (see
C<Raku::Pod::Render>.

The milestone name is the name of the inspection point, and the plugin type.

=head2 Zeroth

Since this is the start of the processing, no plugins are defined as there are no objects for them to operate on.

The C<config.raku> file must exist and must contain a minumum set of keys. It may optionally contain keys for
the control flags that control the stage, see below. The intent is to keep the options for the root configuration file
as small as possible and only refer to the source files. Most other options are configured by the Mode.

During the subsequent B<Source> stage, the source files in the collection are brought in, if the collection has not been
fully initiated, using the C<source-obtain> configaturation list. Alternatively, any updates are brought in
using the C<source-refresh> list. Commonly, sources will be in a I<git> repository, which has separate commands for
C<clone> and C<pull>. If the C<source-obtain> and C<source-refresh> options are not given (for example during a test),
no changes will be made to the source directory.

Any changes to the source files are cached by default.

The control flags for the subsequent process are:
=begin item
B<no-status> (default False)

The compilation and caching of source files is slow, so a progress bar is provided by default. This is not useful when
testing or batch processing, so C<:no-status> prevents the progress bar.

=end item
=begin item
B<no-refresh> (default False)

Prevents source file updates from being brought in.

An explicit configuration or run-time B<no-refresh> = False is over-ridden by
an explicit run-time or configuration B<without-processing> = True.
=end item
=begin item
B<without-processing> (default False)

This option is used to skip every stage upto the B<Completion>, for example starting the document server without
checking for documentation updates or re-rendering templates.

B<without-processing> implies B<no-refresh>, and over-rides any configuration option,
but with the caveat that the caches must exist.
=end item
=begin item
B<recompile>

Forces all the source files to be recompiled into the cache.

B<without-processing> over-rides B<recompile>.
=end item

=head2 Source

At this milestone, the source files have been cached. The B<mode> sub-directory has not been tested, and the configuration
for the mode has not been used. Since plugin management is dependent on the mode configuration, no plugins can be called.

The B<return value> of C<collect> with inspection I<source> inspection points is a single
C<Pod::From::Cache> object that does a C<Post-Cache> role.

A C<Pod::From::Cache> object provides a list of updated files, and a full set of source files. It will
provide a list of Pod::Blocks contained in each content files, using the filename as the key.

A B<source> plugin can associate a C<Pod::Block> with
a key, interpreted as a source filename. Thus the Pod lists can be processed, eg. looking for search keys,
and new pod files can be created that can then be rendered. The filtering is done with the C<Pod-Cache> role. For example,

=begin code
for $cache.list-files {
    my $pod-tree = $cache.pod($_);
    # process pod-tree
    my Pod::Block $processed = something($pod-tree);
    $cache.add("$_\-processed", [ $processed ]); # must be an Array
    $cache.add($_); # omit this step if the unprocessed file is to be rendered
}
=end code

When pod is extracted from the cache in the rendering phase, the C<$_-processed> files will be available, but
the C<$_> files will return an undefined (False in Bool context) and not be rendered.

The next, Mode, stage is when the source files for the Mode are obtained, compiled and cached. The process is controlled by the
same options as the Source stage.

If a sub-directory with the same name as I<mode> does not exist, or there are no config files in the C< <mode>/config> directory,
C<collect> will throw an C<X::Collection::NoMode> exception during this stage.

Mode source files are stored under the mode sub-directory and cached there. If the mode source files are stored remotely and
updated independently of the collection, then the C<mode-obtain> and C<mode-refresh> keys are used.

=head2 Setup

If B<setup> plugins are defined and in the mode's plugins-required<setup> list, then the cache objects for
the sources and the mode's sources (and the B<full-render> value)
are passed to the program defined by the plugin's B<setup> key.

The purpose of this milestone
is to allow for content files to be pre-processed, perhaps to creates several sub-files from one big file, or to combine
files in some way, or to gather information for a search algorithm.

During the setup stage,
=item the C<ProcessedPod> object is prepared,
=item templates specified in the C<templates> directory are added
=item the key B<mode-name> is added to the C<ProcessedPod> object's plugin-data area and given the value of the mode.

The Setup stage depends on the following options:
=item B<no-status> as before, turns off a progress bar
=item B<without-processing> skips the B<setup> stage, unless the caches did not previously exist.
=begin item
B<full-render>

By default, only files that are changed are re-rendered, which includes an assumption that if any source file
is changed, then all the B<mode> sources must be re-rendered as well.

When B<full-render> is True, the output directory is emptied of content, forcing all files to be rendered.

B<full-render> may be combined with B<no-refresh>, for example when templates or plugins are changed and the aim
is to see what effect they have on exactly the same sources. In such a case, the cache will not be changed, but
the cache object will not contain any files generated by B<setup> plugins.

B<without-processsing> takes precedence over B<full-render>, unless there is no output directory.
=end item

=head2 Render

At this milestone C<render> plugins are supplied to the
C<ProcessedPod> object. New Pod::Blocks can be defined, and the templates associated with them can be created.

The source files (by default only those that have been changed) are rendered. 

The stage is controlled by the same options as I<Setup>. So, it can be skipped by setting B<without-processing>.

=head2 Compilation

At this milestone plugins are provided to add compiled data to the C<ProcessedPod> object, so that the sources
in the mode's directory can function.

During the B<Render> stage, the C<%processed> hash is constructed whose keys are the filenames of the output files, and whose
values are a hash of the page components of each page.

The C<compilation> plugins could, eg, collect page component data (eg., Table of Contents, Glossaries, Footnotes),
and write them into the C<ProcessedPod> object separately so there is a TOC, Glossary, etc structure whose keys
are filenames.

The return value of C<collect> at the inspection point is a list of C<ProcessedPod>, C<%process>, with the C<ProcessedPod>
already changed by the C<compilation> plugins.

=head2 Report

Once a collection has been rendered, all the links between files, and to outside targets can be subject to various
tests. It is also possible to subject all the rendered files to tests. This is accomplished using C<report> plugins.

In addition, all the plugins that have been used at each stage (except for the Report stage itself) are available.

The report stage is intended for testing the outputs and producing reports on the tests.

=head2 Completion

Once the collection has been tested, it can be activated. For example, a collection could be processed into a book,
or a C<Cro> App run that makes the rendered files available on a browser. This is done using C<completion> plugins.

The B<no-completion> option allows for the completion phase to be skipped.

Setting B<without-processing> to True and B<no-completion> to True should have no effect unless
=item there are no caches, which will be the case the first time C<collect> is run
=item the destination directory is missing, which will be the case the first time C<collect> is run

Note that the B<no-report> option is False by default, and will take effect even if B<without-processing>
is True, but processing is forced because caches or destination directories are missing.

So this combination is useful to set up the collection and to get a report on the processing.

=head2 Cleanup

Cleanup comes after C<collect> has finished, so is not a part of C<collect>.

Currently, C<collect> just returns with the value of the @plugins-used object.

=head1 Distribution Structure

A distribution contains content files, which may be updated on a regular basis, a cache,
templates, and one or more modes.

=head2 Content

The content of the distribution is contained in B<POD6> files. In addition to the source files, there are
Collection content files which express things like the Table of Contents for the whole collection.

Collection content are held separately to the source content, so that each mode may have different pages.

This allows for active search pages for a Website, not needed for an epub, or publisher data for an output
formation that will be printed.

=head2 Cache

The B<cache> is a Precomp structure into which the content files are pre-preprocessed.

=head2 Mode

The B<Mode> is the collection of templates and configuration for some output.

The default Mode for B<Collection-Raku-Documentation> is B<Website>, for example.

The string defining C<mode> must refer to an immediate directory of the root of the collection,
so it is compared to C</ ^ \W+ (\w+) '/'? .* $ /> and
only the inner C<\w> chars are used.

The templates, configuration, output files, and other assets used by a Mode are associated with the Mode, and should
reside beneath the Mode sub-directory.

=head2 Templates

The B<templates>, which may be any format (currently RakuClosure or Mustache) accepted by
C<ProcessedPod>, define how the following are expressed in the output:
=item the elements of the content files, eg. paragraphs, headers
=item the files as entities, eg, whether as single files, or chapters of a book
=item the collective components of content files, viz, Table of Contents, footnotes, Glossary, Meta data
=item All the templates may be in one file, or distributed between files.
=item2 If there are no templates in the directory, the default files in C<ProcessedPod> are used.
=item2 If there are multiple files in the directory, they will all be evaluated in alphanumeric order.
Note that existing keys will be over-written if they exist in later templates. This is B<not> the same
behaviour as for Configuration files.

=head1 Configuration

There are two levels of configuration. The top-level resides in C<config.raku> in the root directory of the
Collection. The C<collect> sub will fail without this file.

=head2 Top level configuration

In the descriptions below, simple illustrative names are given to files with configuration, templates, callables.
These files are generally B<Raku> programs, which are compiled and run. They will almost certainly contain errors
during development and the B<Rakudo> compiler will provide information based on the filename. So it is good
practice to name the files that make them easier to locate, such as prefixing them with the plugin name.

C<config.raku> B<must> contain the following keys:
=item the B<cache> directory, relative to the root directory of the collection
=item2 C<Collection-Raku-Documentation> default: 'doc-cache',
=item the B<sources> directory, relative to the root of the collection and must contain at least one content file
=item2 C<Collection-Raku-Documentation> default: 'raku-docs'
=item B<mode> is the default mode for the collection, and must be a sub-directory, which must exist and contain
a C<configs> sub-directory (note the plural ending).
=item2 C<Collection-Raku-Documentation> default: 'Website'

The following are optional keys, together with the defaults 
=item the allowed B<extensions> for content files. These are provided to the C<Pod::From::Cache> object.
=item2 < rakudoc pod pod6 p6 pm pm6 >
=item no-status This option controls whether a progress bar is provided in the terminal
=item2 False
=item B<source-obtain> is the array of strings sent to the OS by C<run> to obtain sources, eg git clone
and assumes CWD is set to the directory of collection. Without this key, there must already be files
in C<sources>.

=item2 ()
=item B<source-refresh> is the array of strings run to refresh the sources, assumes CWD set to the directory of sources.
No key assumes the sources never change.

=item2 ()
=item B<ignore> is a list of files in the B<sources> directory that are not cached.
=item2 ()
=item B<no-status> as described in Milestones
=item2 False
=item B<without-processing> as described in Milestones
=item2 False
=item B<no-refresh> as described in Milestones
=item2 False
=item B<recompile> as described in Milestones
=item2 False

=head2 Second-level configuration

The second-level configuration resides in one or more B<files> that are under the B<configs/> sub-directory of the
C<mode> directory. This arrangement is used to allow for configuration to be separated into different named
files for ease of management.

The following rules apply:
=item If the B<configs> directory for a mode does not exist or is empty, B<Raku-Doc> (C<collect> sub) will fail.
=item The Configuration consists of one or more C<Raku> files that each evaluate to a hash.
=item2 Each Config file in the B<Configs> directory will be evaluated in alphabetical order.
=item2 Configuration keys may not be over-written. An C<X::RakuConfig::OverwriteKey> exception will be thrown if
an attempt is made to redefine a key.

All the following keys are mandatory.
Where a key refers to a directory (path), it should normally be relative to the C<mode> sub-directory.
=item B<mode-sources> location of the source files for the Collection pages, eg., TOC.
=item B<mode-cache> location of the cache files
=item the B<templates> subdirectory, which must contain raku files as described in C<ProcessedPod>.
These are all passed at the B<Render> milestone directly to the C<ProcessedPod> object.

=item B<destination> directory where the output files are rendered
=item B<plugins> is a string with the location of the plugins directory, either relative to
root of the mode directory, or an absolute path. It is possible for the plugins directory to contain
unused plugins. See L<Plugin management>

=item B<report-path> is the path to which C<report> plugins should output their reports.
=item B<plugins-required> points to a hash whose keys are milestone names where plugins can be applied
=item2 B<setup> a list of plugin names, see L<Plugin management>, for pre-processing cache contents
=item2 B<render> plugins used to render Pod::Blocks
=item2 B<compilation> plugins prepare the C<ProcessedPod> object for collection pages.
=item2 B<report> plugins to test and report on the rendering process
=item2 B<completion> plugins that define what happens after rendering
=item2 B<cleanup> plugins if cleanup is needed.
=item B<landing-place> is the name of the file that comes first during the completion stage. For example,
in a Website, the landing file is usually called C<index.html>
=item B<output-ext> is the extension for the output files

The following are optional as they are control flags that are False by default.
=item recompile
=item no-refresh
=item full-render
=item no-report
=item without-processing
=item no-cache
=item no-completion
=item debug
=item verbose
=begin item
B<no-code-escape>

C<ProcessedPod> has a special flag for turning off escaping in code sections
when a highlighter is used to pre-process code. In some cases, the highlighter also
does HTML escaping, so RPR has to avoid it.

This has to be done at the Mode level and not left to C<render> plugins.
=end item

=head1 Control flags

The control flags have mostly been described in L<Milestones>. They are summarised here again, with some
extra information.

=begin item
B<recompile>

Controls the updating and caching of the content files. If true, then all files will be recompiled and cached.

A True value is over-ridden by B<without-processing>

Normally False, which allows for only changed files to be processed.

=end item
=begin item
B<no-refresh>

Prevents the updating of content files, so no changes will be made.

=end item
=begin item
B<full-render>

Forces all files to be rendered. Even if there are no changes to source files, plugins or templates
may be added/changed, thus changing the output, so all files need to be re-rendered.

A True value is over-ridden by B<without-processing>

=end item
=begin item
B<no-report>

Normally, report plugins report on the final state of the output files. No-report prevents
report plugins from being loaded or run.

If B<without-processing> is set, then the B<Report> stage is skipped. If, however,
the caches do not exist (deleted or first run), then the value of B<without-processing> is ignored and
the value of B<no-report> is observed.

=end item
=begin item
B<without-processing>

Unless the caches do not exist, setting B<without-processing> to True will skip all the stages except
B<Completion>

=end item
=begin item
B<no-cache>

RakuConfig will cache the previous configuration data by default. When testing a module, this is not desirable,
so no-cache = True prevents caching.

=end item
=begin item
B<debug & verbose>

ProcessedPod uses both debug and verbose, so these flags are passed if set.

=end item
=begin item
B<collection-info>

Causes collect to produce information about milestones and valid and invalid plugins
=end item

=head1 Plugin management

Plugins are B<Raku> programs that are executed at specific milestones in the rendering process.
The milestone are given in L<Milestones> above.

The B<plugins-required> key in the Mode's configuration contains a hash with keys whose names are the
milestone names. Each key points to a list of plugin names, which are called in the order
given.

All plugins must reside within the mode directory given by C<plugins>, but this directory may belong
to another Collection so that plugins can and should be shared between collections & modes.

All plugin names must be the name of a sub-directory of the B<plugins> path. Within each plugin sub-directory,
there must be a C<config.raku> file containing information for the plugin, and for C<Collection>.

With the exception of 'render' plugins, the config file contains a key for the type,
which points to the program to be called.

Plugin's may need other configurable data, which should be kept in the config file for the plugin.

The plugin types are as follows.

=head2 Setup

Config hash must contain B<setup> which is a path, relative to the plugin's sub-directory, to the Raku
program. It must evaluate to a sub that takes a list of five items, eg.,

    sub ( $source-cache, $mode-cache, Bool $full-render,  $source-root, $mode-root ) { ... }

=defn $source-cache
A Pod::From::Cache object containing the pod of the sources files

=defn $mode-cache
Like the above for the mode content files

=defn $full-render
If True, then the sub should process the cache objects with the .sources method on the cache objects,
otherwise with the .list-files method on the cache objects (the .list-files method only
provides the files that have changed).

=defn $source-root
This path must be prepended to any sources added (see below) to the cache, otherwise they will not be added to the
destination file.

=defn $mode-root
Likewise for the mode sources.

New files can be added to the cache object inside the sub using the C<.add> method, see L<Sources>.

=head2 Render

The Collection plugin-manager calls the C<ProcessedPod.add-plugin> method with the config keys and
the path modified to the plugin's subdirectory.

If the C<render> key is True, no callable is provided, and the plugin will be added via the B<.add-plugin>
method of the C<ProcessedPod> object.

If the C<render> key is a Str, then it is the filename of a Raku program that takes a <ProcessedPod> object,
and returns a list of Pairs, with C<key> the value of a path name below the C<%config<destination> > directory
and the C<value> a filename local to the plugin's own subdirectory, which is to be copied to the path in the
key.

So the config file must have:
=item render (True | name of callable)
=item custom-raku => a Raku program that evaluates to an array of custom blocks (must be set to C<()> if no Raku program )
=item template-raku => a Raku program that evaluates to a hash of RakuClosure templates (must be set to C<()> if no Raku program)

It is possible to specify C<path> but it must be relative to the plugin's sub-directory.

More information about these plugins can be found in the documentation in the C<Raku::Pod::Render> distribution.

=head2 Compilation

The C<compilation> key must point to a Raku program that delivers a sub object

    sub ( $pr, %processed) { ... }

=defn $pr
is the ProcessedPod object rendering the content files.

=defn %processed
is a hash whose keys are source file names with a hash values containing
TOC, Glossary, Links, Metadata, Footnotes, Templates-used structures produced by B<ProcessedPod>.

=head2 Report

The C<report> key points to a Raku file that evaluates to a

    sub (%processed, @plugins-used, $report-path) {...}

=defn @plugins-used
is an array of Pairs whose key is the milestone and
value is a hash of the plugins used and their config parameters.

=defn $report-path
is the path name relative to the C<mode> directory where report files are produced. The output format of the files
is determined by the report plugin. 

=defn %processed
as in Compilation

=head2 Completion

The C<completion> key points to a Raku file that evaluates to
a C<sub (@output-files, $destination, $landing-place, $output-ext, %completion-options) {...}> object.

=begin item
@filenames

is a list of the output files (with paths relative to the output path) that are to be presented, in order
of evaluation, if this is important. Eg. for a book, the entire order is important, for a website
ony the first page is important.

=end item
=begin item
$destination

is the name of the output path from the mode directory (defined in the mode configuration)

=end item
=begin item
$landing-place

is the first file to be processed since, eg., for a website, order is not sufficient. name is relative to the
destination directory.

=end item
=begin item
%completion-options> (actually specified as %config<completion-options>)

is the set of options that the completion plugin will require from the Mode-level configuration. For example,
the very simple C<cro-run> plugin requires the path to the static html files, the hostname, and the port on which the files
are served. More complex plugins will require more options.

=end item

=head1 LICENSE

B<LICENSE> Artistic-2.0

=end pod